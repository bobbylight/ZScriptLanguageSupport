apply plugin: 'distribution'

archivesBaseName = 'zscript-demo'

dependencies {
    implementation project(':zscript-lang-support')
    implementation('com.fifesoft.rtext:fife.common:3.0.3-SNAPSHOT') { changing = true }
    implementation files('../lib/RText.jar')
}

jar {
    manifest {
        attributes(
                'Main-Class': 'org.fife.rsta.zscript.demo.DemoApp',
                'Class-Path': configurations.runtimeClasspath.collect {
                    return it.name
                }.join(' ')
        )
    }
}

distributions {
    main {

        distributionBaseName = 'zscript-demo'
        contents {
            exclude([ '**/*-sources.jar', '**/*-javadoc.jar' ])
            from { [ 'build/libs', configurations.runtimeClasspath ] }
            eachFile { details ->
                def path = details.path
                if (path.contains('zscript-demo-') && path.endsWith('.jar')) {
                    details.path = 'zscript-demo.jar'
                }
            }
            duplicatesStrategy = 'include'
        }
    }
    src {
        distributionBaseName = 'zscript-demo'
    }
}

def getJreDependencyList = { installDistDir ->

    // Store the output instead of printing to the console
    def stdout = new ByteArrayOutputStream()

    exec {
        workingDir installDistDir
        commandLine "${jdkRoot}/bin/jdeps", '--list-deps', 'zscript-demo.jar'
        standardOutput = stdout
    }

    def retVal =  String.join(',', stdout.toString().split('[ \t\r\n]+'))
    if (retVal[0] == ',') {
        retVal = retVal.substring(1)
    }
    return retVal
}

task generateJre {
    doLast {

        def installDistDir = 'build/install/zscript-demo'

        def moduleList = getJreDependencyList(installDistDir)
        println "Generating JRE with modules: ${moduleList}"

        exec {
            workingDir installDistDir
            commandLine "${jdkRoot}/bin/jlink", '--module-path', "\"${jdkRoot}/jmods\"", '--add-modules',
                    moduleList, '--output', embeddedJreFolderName, '--strip-debug', '--compress', '2',
                    '--no-header-files', '--no-man-pages'
        }
    }
}
generateJre.dependsOn('installDist')

task buildWindowsDemo {
    doLast {
        exec {
            commandLine launch4jExe, 'launch4j.xml'
        }
    }
}
buildWindowsDemo.dependsOn('generateJre')
